# -*- python -*-

import os
import sys

sys.path.append(Dir('#/scripts').abspath)
import bld_extensions

env = Environment(tools=['default',
                         bld_extensions.TOOL_TEXTURE,
                         bld_extensions.TOOL_MODEL])

# Find all files.
image_formats = ['dds','png','bmp']
for root, dirs, files in os.walk('raw_textures'):
    for file_name in files:
        ext = os.path.splitext(file_name)[1][1:]
        if ext.lower() in image_formats:
            env.Texture(os.path.join(root,file_name))

# Find all models
for root, dirs, files in os.walk('raw_models'):
    for file_name in files:
        ext = os.path.splitext(file_name)[1][1:]
        if ext.lower() == 'blend':
            env.Model(os.path.join(root,file_name))

env.Clean('.',['textures','models'])

resources = [Dir('shaders'),
             Dir('textures'),
             Dir('models')]

Return('resources')
